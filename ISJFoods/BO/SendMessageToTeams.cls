Class ISJFoods.BO.SendMessageToTeams Extends Ens.BusinessOperation
{

/// The type of adapter used to communicate with external systems
Parameter ADAPTER = "EnsLib.HTTP.OutboundAdapter";

Property Adapter As EnsLib.HTTP.OutboundAdapter;

Property token As %String(MAXLEN = 200);

Parameter SETTINGS = "token:teamsè¨­å®š,ReplyCodeActions,RetryInterval,AlertRetryGracePeriod:Alerting,FailureTimeout,QueueCountAlert:Alerting,QueueWaitAlert:Alerting,SendSuperSession";

/// å…¥åŠ›æƒ…å ± {"PID":"P123123","Message":"POPã«è¡¨ç¤ºã—ãŸã„ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸","Status":"pending"} ãªã©
Method POSTString(pRequest As Ens.StringRequest, ByRef pResponse As Ens.StringResponse) As %Status
{
    #dim ex As %Exception.AbstractException
    set status=$$$OK
    try {
        set json={}.%FromJSON(pRequest.StringValue)
        if (json.Status="pending")&(..%ConfigName="å¯©æŸ»å“¡ã¸é€šçŸ¥")  {
            set moji="# POPå€™è£œå±Šãã¾ã—ãŸ"_$$$NL
            set moji=moji_"- PIDï¼š"_json.PID_$$$NL
            set moji=moji_"- ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ï¼š"_json.Message_$$$NL
            set moji=moji_"- [ç¢ºèªãƒšãƒ¼ã‚¸](http://localhost:9093/csp/user/_DeepSee.UserPortal.Workflow.zen)"_$$$NL
            set moji=moji_"- [è©³ç´°ãƒ‡ãƒ¼ã‚¿](http://localhost:5001/product/"_json.PID_")"
        }
        else {
            if json.Status="approved" {
                set moji="# ç™»éŒ²ã—ãŸPOPãŒ**æ‰¿èª**ã•ã‚Œã¾ã—ãŸï¼"_$$$NL
            }
            elseif json.Status="rejected" {
                set moji="# ç™»éŒ²ã—ãŸPOPãŒ**å´ä¸‹**ã•ã‚Œã¾ã—ãŸãƒ»ãƒ»ğŸ˜¢"_$$$NL
            }
            elseif json.Status="pending" {
                set moji="# ç™»éŒ²ã—ãŸPOPãŒ**ä¿ç•™**ã¨ãªã‚Šã¾ã—ãŸã€‚"_$$$NL
            }
            set moji=moji_"- [è©³ç´°ãƒ‡ãƒ¼ã‚¿](http://localhost:5001/product/"_json.PID_")"
        }

        $$$TRACE(moji)

        set originalurl=..Adapter.URL
        if $EXTRACT(originalurl,*)="/" {
            set url=originalurl_..token
        }
        else {
            set url=originalurl_"/"_..token
        }

        set teamsjson={}
        set teamsjson.text=moji

        set stream=##class(%Stream.TmpBinary).%New()
        do teamsjson.%ToJSON(stream)
        //Bodyã«ä½œæˆã—ãŸJSONã‚’å…¥ã‚Œã‚‹å ´åˆã¯ã€PostURL()ãƒ¡ã‚½ãƒƒãƒ‰ç¬¬4å¼•æ•°ã«ã‚¹ãƒˆãƒªãƒ¼ãƒ ã§æŒ‡å®šã™ã‚‹
        $$$ThrowOnError(..Adapter.PostURL(url,.pHttpResponse,,stream))

        if pHttpResponse.StatusCode'=200 {
            set status=$$$ERROR($$$EnsErrGeneral,$$$StatusDisplayString(status)_":"_pHttpResponse.Data.Read())
        }
        $$$ThrowOnError(status)
    }
    catch ex {
        set status=ex.AsStatus()
    }
    return status
}

XData MessageMap
{
<MapItems>
        <MapItem MessageType="Ens.StringRequest">
            <Method>POSTString</Method>
        </MapItem>
    </MapItems>
}

}
